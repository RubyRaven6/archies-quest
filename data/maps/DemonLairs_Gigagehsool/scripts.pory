mapscripts DemonLairs_Gigagehsool_MapScripts {
}

script DemonLairs_Gigagehsool_EventScript_Door {
	lockall
	if(!flag(FLAG_GIGAGEHSOOL_PUZZLE_SOLVED) && var(VAR_TEMP_0) == 0){
		setvar(VAR_TEMP_0, 1)
		msgbox(format("You see a formiddable door before you. It's shut tight, but there's a mechanism written.\p
						Five for Five! Don't forget!\p
						There are shells embedded onto the front door bearing pearls inside them.
						\pAttempt to unlock the door?"),MSGBOX_YESNO)
		if(var(VAR_RESULT) == YES){
			call(DemonLairs_Gigagehsool_EventScript_Puzzle)
		}
	} elif (!flag(FLAG_GIGAGEHSOOL_PUZZLE_SOLVED)){
		call(DemonLairs_Gigagehsool_EventScript_Puzzle)
	} else {
		msgbox("Enter Gigagehsool's Lair?", MSGBOX_YESNO)
		if(var(VAR_RESULT) == YES){warp(MAP_DEMON_LAIRS_GIGAGEHSOOL_INTERIOR, 0, 0)}
	}
	releaseall
}

script DemonLairs_Gigagehsool_EventScript_Puzzle {
	special(OpenGigagehsoolPuzzle)
	waitstate
	if(flag(FLAG_GIGAGEHSOOL_PUZZLE_SOLVED)){
		call(DemonLairs_Gigagehsool_EventScript_DoorIsOpen)
		msgbox("Enter Gigagehsool's Lair?", MSGBOX_YESNO)
		if(var(VAR_RESULT) == YES){warp(MAP_DEMON_LAIRS_GIGAGEHSOOL_INTERIOR, 0, 0)}
	}
	return
}

script DemonLairs_Gigagehsool_EventScript_DoorIsOpen {
	setvar(VAR_0x8004, 1)  // vertical pan
	setvar(VAR_0x8005, 1)  // horizontal pan
	setvar(VAR_0x8006, 8)  // num shakes
	setvar(VAR_0x8007, 5)  // shake delay
	special(ShakeCamera)
	waitstate
	playse(SE_M_ROCK_THROW)
	return
}