mapscripts DemonLairs_Sapprilon_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_0, 0 {
            setvar(VAR_TEMP_0, 1)
            callnative(TrySetSapprilonsLairMusic)
        }
        VAR_DEMON_LORD_PROGRESS, 2 {
            autosave
            setvar(VAR_DEMON_LORD_PROGRESS, 3)
            setflag(FLAG_SYS_BOSS_IN_PROG)
        }
    ]
}

script DemonLairs_Sapprilon_EventScript_CompactDisks {
    msgbox(format("It's a stack of luminescent discs encased in a clear illustrated case."), MSGBOX_SIGN)
    waitmessage
    closemessage
}

script DemonLairs_Sapprilon_EventScript_Monster {
    if(var(VAR_TEMP_0) == 0) {
            msgbox(format(
                "It's a metallic cylinder.\nThere's a large “MONSTER” written on it.\p"
                "Examine the metallic cylinder?"
            ), MSGBOX_YESNO)
        waitmessage
        closemessage
        if(var(VAR_RESULT) == YES){
            msgbox(format(
                "You pick up the cylinder and hear the sloshing of liquid inside.\p"
                "In your infinite curiosity, you take a sip."
            ))
            waitmessage

            playse(SE_PIN)
            applymovement(OBJ_EVENT_ID_PLAYER, moves(emote_upset))

            speakername("Archie")
            msgbox(format("Ugh! This is awful! Must be monster blood to taste so badly."))
            waitmessage
            closemessage
            setvar(VAR_TEMP_0, 1)
        } else {
            msgbox("Feels like a good decision.", MSGBOX_DEFAULT)
            waitmessage
            closemessage
        }
    } else {
        msgbox(format(
            "It's a metallic cylinder.\nThere's a large “MONSTER” written on it."
        ))
        waitmessage
        closemessage
    }
}

script DemonLairs_Sapprilon_EventScript_RegularComputer {
    msgbox(format("It's a machine you don't understand. It has an illustration of a skimpily-dressed woman on it."), MSGBOX_SIGN)
    waitmessage
    closemessage
}

script DemonLairs_Sapprilon_EventScript_Bookshelf1 {
    checkitem(ITEM_ACRO_BIKE, 1)
    if(var(VAR_RESULT) == 0 && !flag(FLAG_SAPPRILON_PUZZLE_SOLVED)){
        msgbox(format("It's a bookshelf wrought from metal.\nA waxen paper slip is sticking out.\pTake it?"), MSGBOX_YESNO)
        closemessage
        waitmessage
        if(var(VAR_RESULT) == YES){
            additem(ITEM_ACRO_BIKE, 1)
            playse(MUS_OBTAIN_ITEM)
            msgbox(format("You got the Black disk!"), MSGBOX_DEFAULT)
            waitmessage
            waitse
            closemessage
        }
    } else {
        msgbox(format("You already have the Black disk."))
        waitmessage
        closemessage
    }
    end
}

script DemonLairs_Sapprilon_EventScript_Bookshelf2 {
    checkitem(ITEM_MACH_BIKE, 1)
    if(var(VAR_RESULT) == 0 && !flag(FLAG_SAPPRILON_PUZZLE_SOLVED)){
        msgbox(format("It's a bookshelf wrought from metal.\nA waxen paper slip is sticking out.\pTake it?"), MSGBOX_YESNO)
        closemessage
        waitmessage
        if(var(VAR_RESULT) == YES){
            additem(ITEM_MACH_BIKE, 1)
            playse(MUS_OBTAIN_ITEM)
            msgbox(format("You got the Yellow disk!"), MSGBOX_DEFAULT)
            waitmessage
            waitse
            closemessage
        }
    } else {
        msgbox(format("You already have the Yellow disk."))
        waitmessage
        closemessage
    }
    end
}

script DemonLairs_Sapprilon_EventScript_Bookshelf3 {
    checkitem(ITEM_SS_TICKET, 1)
    if(var(VAR_RESULT) == 0 && !flag(FLAG_SAPPRILON_PUZZLE_SOLVED)){
        msgbox(format("It's a bookshelf wrought from metal.\nA waxen paper slip is sticking out.\pTake it?"), MSGBOX_YESNO)
        closemessage
        waitmessage
        if(var(VAR_RESULT) == YES){
            additem(ITEM_SS_TICKET, 1)
            playse(MUS_OBTAIN_ITEM)
            msgbox(format("You got the Cyan disk!"), MSGBOX_DEFAULT)
            waitmessage
            waitse
            closemessage
        }
    } else {
        msgbox(format("You already have the Cyan disk."))
        waitmessage
        closemessage
    }
    end
}

script DemonLairs_Sapprilon_EventScript_Bookshelf4 {
    checkitem(ITEM_EON_TICKET, 1)
    if(var(VAR_RESULT) == 0 && !flag(FLAG_SAPPRILON_PUZZLE_SOLVED)){
        msgbox(format("It's a bookshelf wrought from metal.\nA waxen paper slip is sticking out.\pTake it?"), MSGBOX_YESNO)
        closemessage
        waitmessage
        if(var(VAR_RESULT) == YES){
            additem(ITEM_EON_TICKET, 1)
            playse(MUS_OBTAIN_ITEM)
            msgbox(format("You got the Magenta disk!"), MSGBOX_DEFAULT)
            waitmessage
            waitse
            closemessage
        }
    } else {
        msgbox(format("You already have the Magenta disk."))
        waitmessage
        closemessage
    }
    end
}

script DemonLairs_Sapprilon_EventScript_MainComputer {
    playse(SE_PC_ON)
    msgbox(format(
        "An odd contraption of glass and metal.\nStrange words appear!"
    ))
    waitmessage
    closemessage
    dynmultipush("MIDI", 0)
    dynmultipush("DISKS", 1)
    dynmultipush("PRINTER", 2)
    if(flag(FLAG_SAPPRILON_PUZZLE_SOLVED)){dynmultipush("TRANSPORT", 3)}
    dynmultistack(16, 0, FALSE, 10, FALSE, 0, DYN_MULTICHOICE_CB_NONE)

    switch(var(VAR_RESULT)){
        case 0:
            if(!flag(FLAG_SAPPRILON_PUZZLE_SOLVED)){call(DemonLairs_Sapprilon_EventScript_Puzzle)}
            else {
                msgbox(format("The song is very soothing, but you have to go."))
                waitmessage
                closemessage
            }
            break
        case 1:
            if(!flag(FLAG_SAPPRILON_PUZZLE_SOLVED)){call(DemonLairs_Sapprilon_EventScript_DiskInsert)}
            else {
                msgbox("The disks are stuck in the machine!")
                waitmessage
                closemessage
            }
            break
        case 2:
            call(DemonLairs_Sapprilon_EventScript_Printer)
            break
        case 3:
            playse(SE_PC_LOGIN)
            waitse
            delay(48)

            warpwhitefade(MAP_DEMONLAIRS_SAPPRILON_INTERIOR, 5, 9)
    }
}

script DemonLairs_Sapprilon_EventScript_Puzzle {
    call(DemonLairs_ItemChecker)
    msgbox("Launch the program?", MSGBOX_YESNO)
    closemessage
    if(var(VAR_RESULT) == TRUE && var(VAR_TEMP_1) == 1 && var(VAR_TEMP_2) == 1 && var(VAR_TEMP_3) == 1 && var(VAR_TEMP_4) == 1){
        msgbox(format("Which disk will you select first?"))
        call(DemonLairs_DiskInsertPuzzle)
    } elif (var(VAR_RESULT) == NO){

    } else {
        msgbox(format("Looks like you can't solve this yet."))
        closemessage
    }
}

script DemonLairs_Sapprilon_EventScript_DiskInsert{
    playse(SE_PC_LOGIN)

    call(DemonLairs_ItemChecker)
    if(var(VAR_TEMP_1) == 1 || var(VAR_TEMP_2) == 1 || var(VAR_TEMP_3) == 1 ||var(VAR_TEMP_4) == 1 ){
        if(var(VAR_TEMP_1) == TRUE){
            dynmultipush("Insert Black disk", 3)
        }
        if(var(VAR_TEMP_2) == TRUE){
            dynmultipush("Insert Yellow disk", 2)
        }
        if(var(VAR_TEMP_3) == TRUE){
            dynmultipush("Insert Cyan disk", 0)
        }
        if(var(VAR_TEMP_4) == TRUE){
            dynmultipush("Insert Magenta disk", 1)
        }

        dynmultistack(16, 0, FALSE, 4, FALSE, 4, DYN_MULTICHOICE_CB_NONE)
        switch(var(VAR_RESULT)){
            case 0:
                fadeoutbgm(0)
                playbgm(MUS_SAPPRILON1, FALSE)
                fadeoutbgm(0)
                fadedefaultbgm
            case 1:
                fadeoutbgm(0)
                playbgm(MUS_SAPPRILON2, FALSE)
                fadeoutbgm(0)
                fadedefaultbgm
            case 2:
                fadeoutbgm(0)
                playbgm(MUS_SAPPRILON3, FALSE)
                fadeoutbgm(0)
                fadedefaultbgm
            case 3:
                fadeoutbgm(0)
                playbgm(MUS_SAPPRILON4, FALSE)
                fadeoutbgm(0)
                fadedefaultbgm
        }
    } else {
        msgbox(format("Looks like you have to insert something for this option to work."), MSGBOX_SIGN)
        waitmessage
        closemessage
    }
}

script DemonLairs_Sapprilon_EventScript_Printer {
    playse(SE_PC_LOGIN)

    msgbox(format(
        "…{PAUSE 16}…{PAUSE 16}…{PAUSE 16}…\p"
        "“PRINTER CANNOT COMPLETE OPERATION\nPLEASE REFILL CMYK CARTRIDGES”"
    ))
    waitmessage
    closemessage
}

script(local) DemonLairs_ItemChecker {
    checkitem(ITEM_ACRO_BIKE, 1)
    copyvar(VAR_TEMP_1, VAR_RESULT)
    checkitem(ITEM_MACH_BIKE,1)
    copyvar(VAR_TEMP_2, VAR_RESULT)
    checkitem(ITEM_SS_TICKET,1)
    copyvar(VAR_TEMP_3, VAR_RESULT)
    checkitem(ITEM_EON_TICKET,1)
    copyvar(VAR_TEMP_4, VAR_RESULT)
    return
}

script(local) DemonLairs_DiskInsertPuzzle {
    call(DemonLairs_ItemChecker)
    dynmultichoice(16, 0, FALSE, 4, 0, DYN_MULTICHOICE_CB_NONE, "Insert Black disk", "Insert Yellow disk", "Insert Cyan disk", "Insert Magenta disk")
    if(var(VAR_TEMP_1) == 1 || var(VAR_TEMP_2) == 1 || var(VAR_TEMP_3) == 1 || var(VAR_TEMP_4) == 1)
    {
        switch(var(VAR_RESULT)){
        case 0:
            call(DemonLairs_ItemChecker)
            if(var(VAR_TEMP_1) == 1){
                sappypuzzle(1)
                removeitem(ITEM_ACRO_BIKE, 1)
                playse(SE_SWITCH)
                msgbox("Inserted Black disk.")
                waitmessage
                closemessage
                if(var(VAR_TEMP_2) == 1 || var(VAR_TEMP_3) == 1 || var(VAR_TEMP_4) == 1){
                    call(DemonLairs_DiskInsertPuzzle)
                } else {
                    goto(WinCondition)
                }
            } else {
                msgbox("You already inserted that.")
                waitmessage
                closemessage
                call(DemonLairs_DiskInsertPuzzle)
            }
            break
        case 1:
            call(DemonLairs_ItemChecker)
            if(var(VAR_TEMP_2) == 1){
                sappypuzzle(2)
                removeitem(ITEM_MACH_BIKE, 1)
                playse(SE_SWITCH)
                msgbox("Inserted Yellow disk.")
                waitmessage
                closemessage
                if(var(VAR_TEMP_2) == 1 || var(VAR_TEMP_3) == 1 || var(VAR_TEMP_4) == 1){
                    call(DemonLairs_DiskInsertPuzzle)
                } else {
                    goto(WinCondition)
                }
            } else {
                msgbox("You already inserted that.")
                waitmessage
                closemessage
                call(DemonLairs_DiskInsertPuzzle)
            }
            break
        case 2:
            call(DemonLairs_ItemChecker)
            if(var(VAR_TEMP_3) == 1){
                sappypuzzle(3)
                removeitem(ITEM_SS_TICKET, 1)
                playse(SE_SWITCH)
                msgbox("Inserted Cyan disk.")
                waitmessage
                closemessage
                if(var(VAR_TEMP_1) == 1 || var(VAR_TEMP_2) == 1 || var(VAR_TEMP_4) == 1){
                    call(DemonLairs_DiskInsertPuzzle)
                } else {
                    goto(WinCondition)
                }
            } else {
                msgbox("You already inserted that.")
                waitmessage
                closemessage
                call(DemonLairs_DiskInsertPuzzle)
            }
            break
        case 3:
            call(DemonLairs_ItemChecker)
            if(var(VAR_TEMP_4) == 1){
                sappypuzzle(4)
                removeitem(ITEM_EON_TICKET, 1)
                playse(SE_SWITCH)
                msgbox("Inserted Magenta disk.")
                closemessage
                if(var(VAR_TEMP_1) == 1 || var(VAR_TEMP_2) == 1 || var(VAR_TEMP_3) == 1){
                    call(DemonLairs_DiskInsertPuzzle)
                } else {
                    goto(WinCondition)
                }
            } else {
                msgbox("You already inserted that.")
                waitmessage
                closemessage
                call(DemonLairs_DiskInsertPuzzle)
            }
            break
        }
    }
    goto(EarlyExit)
}

script(local) EarlyExit{
    call(DemonLairs_ItemChecker)
    if(var(VAR_TEMP_1) < 1){additem(ITEM_ACRO_BIKE,  1)}
    if(var(VAR_TEMP_2) < 1){additem(ITEM_MACH_BIKE,  1)}
    if(var(VAR_TEMP_3) < 1){additem(ITEM_SS_TICKET,  1)}
    if(var(VAR_TEMP_4) < 1){additem(ITEM_EON_TICKET, 1)}
    setvar(VAR_SAPPRILON_PUZZLE_VAL, 0)
    end
}

script(local) WinCondition {
    if (var(VAR_SAPPRILON_PUZZLE_VAL) == 3421) {
        setflag(FLAG_SAPPRILON_PUZZLE_SOLVED)

        msgbox(format(
            "…{PAUSE 16}…{PAUSE 16}…{PAUSE 16}…"
        ))
        closemessage

        playse(SE_SUCCESS)
        waitse

        delay(48)

        msgbox(format(
            "Hmm…\p"
            "Did something happen?"
        ))

        fadeoutbgm(0)
        playbgm(MUS_ROUTE101, FALSE)

        applymovement(OBJ_EVENT_ID_PLAYER, moves(
            delay_16 * 3,
            walk_in_place_fast_down,
            delay_16 * 2,
            face_right,
            lock_facing_direction,
            walk_left,
            unlock_facing_direction,
            face_left,
            lock_facing_direction,
            walk_right,
            unlock_facing_direction,
            face_right,
            lock_facing_direction,
            walk_left,
            unlock_facing_direction,
            face_left,
            lock_facing_direction,
            walk_right,
            unlock_facing_direction,
            face_down,
            delay_16 * 2
        ))
        waitmovement(0)

        msgbox(format(
            "The song is surprisingly catchy."
        ))
        closemessage

        autosave
    } else {
        setvar(VAR_SAPPRILON_PUZZLE_VAL, 0)

        msgbox(format(
            "…{PAUSE 16}…{PAUSE 16}…{PAUSE 16}…\p"
        ))
        closemessage

        playse(SE_FAILURE)
        waitse

        delay(48)

        msgbox(format(
            "Hmm…\p"
            "Nothing happened."
        ))
        closemessage
    }

    release
}
