mapscripts DemonLairs_Nessie_Interior_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_FIRST_TIME_ENTERING_NESSIE, 0 {
            lockall
            autosave
            setvar(VAR_FIRST_TIME_ENTERING_NESSIE, 1)
            delay(24)
            playse(SE_CONTEST_CONDITION_LOSE)
            waitse
            playse(SE_PIN)
            applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_fast_down emote_exclamation_mark, delay_16 * 6))
            waitmovement(0)
            waitse

            msgbox(format(
                "… …The entrance has closed."
            ))

            closemessage
            releaseall
        }
    ]
}

script DemonLairs_EventScript_Nessie {
    lock
    faceplayer
    if(!flag(FLAG_TALKED_TO_NESSEREIGN)){
        setflag(FLAG_TALKED_TO_NESSEREIGN)
        playse(SE_PIN)
        applymovement(VAR_LAST_TALKED, moves(emote_upset))
        speakername("Nessereign")
        msgbox(format(
            "What in the nine hells is that RACKET?! Can't you see I'm WORKING here?\p"
            "Right when that SPLITTING HEADACHE was starting to wane…"
        ))
        closemessage

        playse(SE_PIN)
        applymovement(LOCALID_NESSIE_PAINTER, moves(emote_exclamation_mark, delay_16 * 6))
        waitmovement(0)

        speakername("Nessereign")
        msgbox(format(
            "Weeeell, who might this pretty young thing be?\p"
            "I'm {COLOR RED}Nessereign{COLOR DARK_GRAY}.\nBut YOU, you can call me {COLOR RED}Nessie{COLOR DARK_GRAY}!\p"
            "Are you lost?\nYou delicious,{PAUSE 8} GODFORSAKEN{PAUSE 8} INTRUDER?"
        ))
        closemessage

        playse(SE_LEDGE)
        applymovement(LOCALID_PLAYER, moves(lock_facing_direction, jump_in_place_down, unlock_facing_direction))
        waitmovement(0)

        speakername("Archie")
        msgbox(format(
            "You {COLOR RED}Bai'Narii{COLOR DARK_GRAY} tyrants are about to meet your end, {COLOR RED}Nessereign{COLOR DARK_GRAY}!"
        ))
        closemessage

        playse(SE_PIN)
        applymovement(LOCALID_NESSIE_PAINTER, moves(emote_happy, shake_vertical))
        speakername("Nessereign")
        msgbox(format(
            "Ha ha ha ha ha…"
        ))
        waitmovement(LOCALID_NESSIE_PAINTER)
        applymovement(LOCALID_NESSIE_PAINTER, moves(shake_vertical * 2))
        msgbox(format(
            "HA HA HA HA HA\nHA HA HA HA HA!!\p"
            "You made me laugh so hard the headaches have come!{PAUSE 8} Right!{PAUSE 8} BACK!\p"
            "If you think I'll let you get away, you have another thing coming!\p"
            "Surprise, surprise…{PAUSE 16} IT'S DEATH!"
        ))
        waitmessage
        closemessage
    } else {
        playse(SE_PIN)
        applymovement(LOCALID_NESSIE_PAINTER, moves(emote_happy))
        speakername("Nessereign")
        msgbox(format(
            "You just don't.{PAUSE 8} Know.{PAUSE 8} WHEN to quit, huh?\p"
            "My sweet, sweet friend…\nYou're in luck! Neither do I!"
        ))
        waitmessage
        closemessage
    }

    trainerbattle_no_intro(TRAINER_DEMONLORD_NESSEREIGN, format("No! This can't be!\nAugh… My head…"))

    playbgm(MUS_RAYQUAZA_APPEARS, TRUE)
    playse(SE_PIN)
    applymovement(LOCALID_NESSIE_PAINTER, moves(emote_sad))
    speakername("Nessereign")
    msgbox(format(
        "Impossible…{PAUSE 16} You HEATHEN…{PAUSE 16}\nThis simply CAN-{PAUSE 8}NOT-{PAUSE 8}BE…"        
    ))

    playse(SE_M_THUNDERBOLT2)
	setvar(VAR_0x8004, 0)  // vertical pan
	setvar(VAR_0x8005, 1)  // horizontal pan
	setvar(VAR_0x8006, 5)  // num shakes
	setvar(VAR_0x8007, 3)  // shake delay
	special(ShakeCamera)
    
    speakername("Nessereign")
    msgbox(format(
        "I was standing here, painting before you were even BORN!\p"
        "I have painted{PAUSE 8} EVERYTHING your eyes will ever SEE!"
    ))

    playse(SE_M_THUNDERBOLT2)
	setvar(VAR_0x8004, 0)  // vertical pan
	setvar(VAR_0x8005, 1)  // horizontal pan
	setvar(VAR_0x8006, 8)  // num shakes
	setvar(VAR_0x8007, 3)  // shake delay
	special(ShakeCamera)

    speakername("Nessereign")
    msgbox(format(
        "The… The headaches…\n"
        "The h-headaches…{PAUSE 8} are so painful…\p"
        "Huuuuh…{PAUSE 24} {FONT_SMALL}H-Help{FONT_NORMAL}…{PAUSE 16}"
    ))
    closemessage

    applymovement(LOCALID_NESSIE_PAINTER, moves(shake_horizontal))
    playbgm(MUS_DUMMY, TRUE)
    playmoncry(SPECIES_AURORUS, CRY_MODE_WEAK)
    waitmovement(LOCALID_NESSIE_PAINTER)

	setvar(VAR_0x8004, 0)  // vertical pan
	setvar(VAR_0x8005, 1)  // horizontal pan
	setvar(VAR_0x8006, 8)  // num shakes
	setvar(VAR_0x8007, 3)  // shake delay
	special(ShakeCamera)

    fadescreen(FADE_TO_WHITE)

    removeobject(LOCALID_NESSIE_PAINTER)

    delay(60)

    fadescreen(FADE_FROM_WHITE)

    delay(60)

    clearflag(FLAG_SYS_BOSS_IN_PROG)

    playse(SE_PC_ON)
    setmetatile(4, 5, 0x220, FALSE)
    special(DrawWholeMapView)
    waitse

    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, moves(face_left, emote_question_mark, delay_16 * 6))
    waitmovement(0)

    speakername("Old Archie")
    msgbox(format(
        "Fshh… Krrshr…\p"
        "Kshh… Archie, this is cooler Archie, do you copy?\p"
        "Kshhfsh… Finally tracked down your location…\p"
        "That means you've beaten {COLOR RED}Nessereign{COLOR DARK_GRAY}!\nCongrats, kid!\p"
        "You can take the portal I opened back to the hub.\p"
        "Archie, over and out."
    ))
    closemessage

    playse(SE_POKENAV_HANG_UP)
    waitse

    release
}

script DemonLairs_Nessie_Interior_EventScript_InvertedMelon {
    msgbox(format("You look curiously at the painting. It seems to be a depiction of a ruptured watermelon."), MSGBOX_DEFAULT)
    waitmessage
    closemessage
}

script DemonLairs_Nessie_Interior_EventScript_RedSea {
    msgbox(format("It's an illustration of a raging, blood red ocean."), MSGBOX_DEFAULT)
    waitmessage
    closemessage
}

script DemonLairs_Nessie_Interior_EventScript_FamiliarPainting {
    msgbox(format("You have the feeling you saw this painting before. Was it in a mine?\lOr a craftsman's house?"), MSGBOX_DEFAULT)
    waitmessage
    closemessage
    applymovement(LOCALID_PLAYERS_HOUSE_1F_DAD, moves(face_original_direction))
}
