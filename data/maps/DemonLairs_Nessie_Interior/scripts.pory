mapscripts DemonLairs_Nessie_Interior_MapScripts {
}

script DemonLairs_EventScript_Nessie {
    if(!flag(FLAG_TALKED_TO_NESSEREIGN)){
        setflag(FLAG_TALKED_TO_NESSEREIGN)
        speakername("Nessereign")
        msgbox(format("What in the nine hells is that racket?! Can't you see I'm working?"),MSGBOX_DEFAULT)
        waitmessage
        closemessage
        faceplayer
        applymovement(LOCALID_NESSIE_PAINTER, moves(emote_double_exclamation_mark))
        speakername("Nessereign")
        msgbox(format("A-An intruder?! No… it can't be…"), MSGBOX_DEFAULT)
        waitmessage
        closemessage
        applymovement(LOCALID_PLAYER, moves(walk_in_place_up))
        speakername("Archie")
        msgbox(format("You tyrants are about to meet your end, Nessereign!"), MSGBOX_DEFAULT)
        waitmessage
        closemessage
        applymovement(LOCALID_NESSIE_PAINTER, moves(walk_in_place_down))
        speakername("Nessereign")
        msgbox(format("If you think I'll let you get away, you have another thing coming!"), MSGBOX_DEFAULT)
        waitmessage
        closemessage
    } else {
        speakername("Nessereign")
        msgbox(format("You just don't know when to quit, huh? Luckily neither do I!"), MSGBOX_DEFAULT)
        waitmessage
        closemessage
    }
    trainerbattle_no_intro(TRAINER_DEMONLORD_NESSEREIGN, format("No! This can't be!"))
    speakername("Nessereign")
    msgbox("Argh!! You'll regret this!!\pNOOOOOOOO!")
    fadescreen(FADE_TO_WHITE)
    removeobject(LOCALID_NESSIE_PAINTER)
    waitmessage
    closemessage
    fadescreen(FADE_FROM_WHITE)
    delay(60)
    clearflag(FLAG_SYS_BOSS_IN_PROG)
    playse(SE_PC_ON)
    applymovement(LOCALID_PLAYER, moves(face_left))
    setmetatile(4, 5, 0x220, FALSE)
    special(DrawWholeMapView)
    waitse
    speakername("Old Archie")
    msgbox(format("Hey, kid! I can finally get a handle of where you are; that means you must have beaten Nessereign!\p"
                    "I've opened up a portal back to the hub whenever you need it."))
    waitmessage
    waitmovement(0)
    closemessage
    end
}

script DemonLairs_Nessie_Interior_EventScript_InvertedMelon {
    msgbox(format("You look curiously at the painting. It seems to be a depiction of a ruptured watermelon."), MSGBOX_DEFAULT)
    waitmessage
    closemessage
}

script DemonLairs_Nessie_Interior_EventScript_RedSea {
    msgbox(format("It's an illustration of a raging, blood red ocean."), MSGBOX_DEFAULT)
    waitmessage
    closemessage
}

script DemonLairs_Nessie_Interior_EventScript_FamiliarPainting {
    msgbox(format("You have the feeling you saw this painting before. Was it in a mine? Or a craftsman's house?"), MSGBOX_DEFAULT)
    waitmessage
    closemessage
    applymovement(LOCALID_PLAYERS_HOUSE_1F_DAD, moves(face_original_direction))
}
