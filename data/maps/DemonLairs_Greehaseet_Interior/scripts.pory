mapscripts DemonLairs_Greehaseet_Interior_MapScripts {

}

script DemonLairs_EventScript_Greehaseet {
    if(!flag(FLAG_TALKED_TO_GREEHASEET)){
        setflag(FLAG_TALKED_TO_GREEHASEET)
        speakername("Greehaseet")
        msgbox(format("Hmmm. So what I heard was right.\nOur sister {COLOR RED}Nessereign{COLOR DARK_GRAY} was defeated."))
        closemessage

        playse(SE_PIN)
        applymovement(LOCALID_PLAYER, moves(emote_angry))
        speakername("Archie")
        msgbox(format("That's right, Demon Lord! Your reigns of terror are gonna end by my hand!"), MSGBOX_DEFAULT)
        closemessage

        speakername("Greehaseet")
        msgbox(format(
            "Heahaheaha. You naive child.\p"
            "I will take immense pleasure in skewering you like a fresh eel.\p"
            "Prepare to drown!"
        ))
        closemessage
    } else {
        faceplayer
        speakername("Greehaseet")
        msgbox(format("Huehue. I can do this for longer than you have years in your life, boy!"), MSGBOX_DEFAULT)
        closemessage
    }

    trainerbattle_no_intro(TRAINER_DEMONLORD_GREEHASEET, format("No! I crashed out of my own waves?!"))

    playbgm(MUS_RAYQUAZA_APPEARS, TRUE)

    speakername("Greehaseet")
    msgbox(format("This c-cannot be… I've reigned upon these depths since the very beginning of time…"))
    
    playse(SE_M_THUNDERBOLT2)
	setvar(VAR_0x8004, 0)  // vertical pan
	setvar(VAR_0x8005, 1)  // horizontal pan
	setvar(VAR_0x8006, 5)  // num shakes
	setvar(VAR_0x8007, 3)  // shake delay
	special(ShakeCamera)

    msgbox(format(
        "I am he who swallows all!\n"
        "I am the devourer of matter and worlds!\p"
        "I cannot be defeated, for I am everything! I--\p"
        "AUUUUGHHHHH!!!"
    ))
    closemessage

    applymovement(LOCALID_GREEHASEET_SWIMMER, moves(shake_horizontal))
    playbgm(MUS_DUMMY, TRUE)
    playmoncry(SPECIES_GREEHASEET, CRY_MODE_WEAK)
    waitmovement(LOCALID_GREEHASEET_SWIMMER)

	setvar(VAR_0x8004, 0)  // vertical pan
	setvar(VAR_0x8005, 1)  // horizontal pan
	setvar(VAR_0x8006, 8)  // num shakes
	setvar(VAR_0x8007, 3)  // shake delay
	special(ShakeCamera)

    fadescreen(FADE_TO_WHITE)

    removeobject(LOCALID_GREEHASEET_SWIMMER)

    delay(60)

    fadescreen(FADE_FROM_WHITE)

    delay(60)

    clearflag(FLAG_SYS_BOSS_IN_PROG)

    playse(SE_PC_ON)
    setmetatile(8, 10, 0x014, FALSE)
    special(DrawWholeMapView)
    waitse

    applymovement(LOCALID_PLAYER, moves(face_down))
    end
}
