mapscripts DemonLairs_Nessie_MapScripts{
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_DEMON_LORD_PROGRESS, 0 {
            autosave
            setvar(VAR_DEMON_LORD_PROGRESS, 1)
            setflag(FLAG_SYS_BOSS_IN_PROG)
        }
    ]
    MAP_SCRIPT_ON_TRANSITION {
        if(flag(FLAG_NESSIE_PUZZLE_SOLVED)){
            setmetatile(2, 0, METATILE_NessereignLairPrimary_ENTRANCE_UPL, TRUE) //opens hole
            setmetatile(3, 0, METATILE_NessereignLairPrimary_ENTRANCE_UPM, TRUE)
            setmetatile(4, 0, METATILE_NessereignLairPrimary_ENTRANCE_UPR, TRUE)
            setmetatile(2, 1, METATILE_NessereignLairPrimary_ENTRANCE_ML, TRUE)
            setmetatile(3, 1, METATILE_NessereignLairPrimary_ENTRANCE_MM, TRUE)
            setmetatile(4, 1, METATILE_NessereignLairPrimary_ENTRANCE_MR, TRUE)
            setmetatile(2, 2, METATILE_NessereignLairPrimary_ENTRANCE_DNL, TRUE)
            setmetatile(3, 2, METATILE_NessereignLairPrimary_ENTRANCE_DNM, FALSE)
            setmetatile(4, 2, METATILE_NessereignLairPrimary_ENTRANCE_DNR, TRUE)

            setmetatile(3, 7, METATILE_NessereignLairPrimary_TableCorner, TRUE) // hides knife
        }
    }
}

script DemonLairs_Nessie_EventScript_NessiePuzzle{
    if(!flag(FLAG_NESSIE_PUZZLE_SOLVED)){
        msgbox("It's an impressive painting.\nView it closer?", MSGBOX_YESNO)
        closemessage
        if(var(VAR_RESULT) == YES){
            special(OpenNessiePuzzle)
        }
        waitstate
        if(flag(FLAG_NESSIE_PUZZLE_SOLVED)){
            lockall
            msgbox("The wall is collapsing!", MSGBOX_DEFAULT)
            waitmessage
            closemessage
            playse(SE_M_ROCK_THROW)
            setvar(VAR_0x8004, 1)  // vertical pan
            setvar(VAR_0x8005, 0)  // horizontal pan
            setvar(VAR_0x8006, 8)  // num shakes
            setvar(VAR_0x8007, 3)  // shake delay
            special(ShakeCamera)
            waitstate
            setmetatile(2, 0, METATILE_NessereignLairPrimary_ENTRANCE_UPL, TRUE)
            setmetatile(3, 0, METATILE_NessereignLairPrimary_ENTRANCE_UPM, TRUE)
            setmetatile(4, 0, METATILE_NessereignLairPrimary_ENTRANCE_UPR, TRUE)
            setmetatile(2, 1, METATILE_NessereignLairPrimary_ENTRANCE_ML, TRUE)
            setmetatile(3, 1, METATILE_NessereignLairPrimary_ENTRANCE_MM, TRUE)
            setmetatile(4, 1, METATILE_NessereignLairPrimary_ENTRANCE_MR, TRUE)
            setmetatile(2, 2, METATILE_NessereignLairPrimary_ENTRANCE_DNL, TRUE)
            setmetatile(3, 2, METATILE_NessereignLairPrimary_ENTRANCE_DNM, FALSE)
            setmetatile(4, 2, METATILE_NessereignLairPrimary_ENTRANCE_DNR, TRUE)
            special(DrawWholeMapView)
            msgbox(format("The yawning entrance to an antechamber has opened.\pEnter?"), MSGBOX_YESNO)
            waitmessage
            closemessage
            if(var(VAR_RESULT) == YES) {warp(MAP_DEMON_LAIRS_NESSIE_INTERIOR, 3, 8)}
        }
    } else {
        msgbox(format("The yawning entrance to an antechamber has opened.\pEnter?"), MSGBOX_YESNO)
        waitmessage
        closemessage
        if(var(VAR_RESULT) == YES) {warp(MAP_DEMON_LAIRS_NESSIE_INTERIOR, 3, 8)}
    }
    end
}

script DemonLairs_Nessie_EventScript_Knife {
    if(flag(FLAG_NESSIE_GOT_DAGGER) == FALSE){
        msgbox("It's a small dagger.\nTake it?", MSGBOX_YESNO)
        if(var(VAR_RESULT) == YES){
            waitmessage
            setflag(FLAG_NESSIE_GOT_DAGGER)
            setmetatile(3, 7, METATILE_NessereignLairPrimary_TableCorner, TRUE)
            special(DrawWholeMapView)
            closemessage
        }
    } else {
        goto(DemonLairs_Nessie_EventScript_Table)
    }
    end
}

script DemonLairs_Nessie_EventScript_Book {
    if(!flag(FLAG_NESSIE_READ_BOOK))
    {
        msgbox(format(
            "It's an ancient tome with pages yellow with age.\p"
            "There are no words, but merely looking at it makes you feel nauseous.\p"
            "Continue?"
        ), MSGBOX_YESNO)
        if(var(VAR_RESULT) == YES)
        {
            msgbox(format(
                "You thumb through and find gross and disgusting images.\p"
                "You strain until you see a phrase:\p"
                "“Sheathing sheathing sheathing sheathing sheathing sheathing\p"
                "Sharpness sharpness sharpness sharpness sharpness sharpness\p"
                "Through through through through through through through through\p"
                "Screams screams screams screams screams screams screams screams”\p"
                "It feels like you learned something important."
            ))
            setflag(FLAG_NESSIE_READ_BOOK)
            waitmessage
            closemessage
        }
        waitmessage
        closemessage
    }
    else{
        msgbox(format(
            "“Sheathing sheathing sheathing sheathing sheathing sheathing\p"
            "Sharpness sharpness sharpness sharpness sharpness sharpness\p"
            "Through through through through through through through through\p"
            "Screams screams screams screams screams screams screams screams”\p"
            "It's a dizzying tome.\nNo need to see the rest."
        ))
        waitmessage
        closemessage
    }
    end
}

script DemonLairs_Nessie_EventScript_Fireplace {
    msgbox(format("It's a fireplace.\nThere's ash, but no sign of burning."), MSGBOX_SIGN)
    closemessage
    end
}

script DemonLairs_Nessie_EventScript_Chair {
    switch(var(VAR_TEMP_1)){
        case 0:
            msgbox("It's a humongous chair.\nFar too big for you.", MSGBOX_SIGN)
            addvar(VAR_TEMP_1, 1)
        case 1:
            msgbox("Thinking about how big the chair looks\nis giving you a headache.", MSGBOX_SIGN)
            addvar(VAR_TEMP_1, 1)
        default:
            msgbox("Thinking about how the table looks\nnormal but the chair doesn't gives you\lan even bigger headache.", MSGBOX_SIGN)
    }
    waitmessage
    closemessage
    end
}

script DemonLairs_Nessie_EventScript_Table {
    switch(var(VAR_TEMP_2)){
        case 0:
            msgbox("It's an impressively made table.", MSGBOX_SIGN)
            addvar(VAR_TEMP_2, 1)
        case 1:
            msgbox("Unlike everything else in the room, you\nactually quite like the table.", MSGBOX_SIGN)
            addvar(VAR_TEMP_2, 1)
        default:
            msgbox("You wish you could take the table home,\nbut alas, it'd clash with your decor.", MSGBOX_SIGN)
    }
    waitmessage
    closemessage
    end
}

script DemonLairs_Nessie_EventScript_Rug {

    switch(var(VAR_TEMP_3)){
        case 0:
            msgbox("It's a garish rug.", MSGBOX_SIGN)
            addvar(VAR_TEMP_3, 1)
        case 1:
            msgbox("It's still a garish rug.", MSGBOX_SIGN)
            addvar(VAR_TEMP_3, 1)
        case 2:
            msgbox("To no one's surprise, it's a garish rug.", MSGBOX_SIGN)
            addvar(VAR_TEMP_3, 1)
        case 3:
            msgbox("Unastonishingly, it's a garish rug.", MSGBOX_SIGN)
            addvar(VAR_TEMP_3, 1)
        default:
            msgbox("It's not getting any less garish.\nYou should move on.", MSGBOX_SIGN)
            addvar(VAR_TEMP_3, 1)
    }
    waitmessage
    closemessage
    end
}
