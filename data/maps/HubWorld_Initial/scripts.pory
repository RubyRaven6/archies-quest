mapscripts HubWorld_Initial_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        switch(var(VAR_DEMON_LORD_PROGRESS)){
            case 0:
                setmaplayoutindex(LAYOUT_HUB_WORLD_INITIAL) // First Time
            case 1:
                setmaplayoutindex(LAYOUT_HUB_WORLD_NESSIE_DEFEATED) // After Defeating Nessereign
            case 2:
                setmaplayoutindex(LAYOUT_HUB_WORLD_GREEHASEET_DEFEATED) // After defeating Greehaseet
            default:
                setmaplayoutindex(LAYOUT_HUB_WORLD_SAPPRILON_DEFEATED) // After defeating Sapprilon and Addisamap
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_0, 0 {
            if(var(VAR_DEMON_LORD_PROGRESS) == 4){}else{autosave}
            callnative(LevelMonsToCap)
            special(HealPlayerParty)
            setvar(VAR_TEMP_0, 1)
        }
        VAR_HUB_WORLD_INITIAL_SCENE, 0 {
            applymovement(LOCALID_HUBWORLD_ARCHIE, moves(walk_in_place_fast_down))
            applymovement(LOCALID_PLAYER, moves(walk_in_place_fast_up))
            waitmovement(LOCALID_HUBWORLD_ARCHIE)
            waitmovement(LOCALID_PLAYER)

            special(SpawnCameraObject)

            speakername("Old Archie")
            msgbox(format(
                "{CREATE_MUGSHOT MUGSHOT_OLD_ARCHIE 0}This is going to be our base of operations."
            ))

            applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_down * 4, delay_16 * 3, walk_up * 4))

            msgbox(format(
                "It's a pocket dimension shrouded from the influence of the {COLOR RED}Bai'Narii{COLOR DARK_GRAY}.\p"
                "I opened a portal using the magical anchor object behind you.\p"
                "When you're ready, touch it and you'll be taken to a Demon Lord's lair.\p"
                "I'll be able to open a new portal back here once you defeat a Demon Lord.\p"
                "Until then, you're on your own.\nThe only way out is through!"
            ))
            applymovement(LOCALID_HUBWORLD_ARCHIE, moves(walk_in_place_fast_right))
            applymovement(OBJ_EVENT_ID_PLAYER, moves(delay_16 * 2, face_right))
            msgbox(format(
                "By the by, I stored some useful items in that futuristic device in the corner."
            ))
            applymovement(LOCALID_HUBWORLD_ARCHIE, moves(walk_in_place_fast_down))
            applymovement(OBJ_EVENT_ID_PLAYER, moves(delay_16 * 2, face_up))
            msgbox(format(
                "Well then, break a leg! If you need any help or hints, I'll just be waiting here, doing nothing, I mean, surveiling the area."
            ))
            closemessage

            setvar(VAR_HUB_WORLD_INITIAL_SCENE, 1)
            special(RemoveCameraObject)
        }
        VAR_DEMON_LORD_PROGRESS, 4 {
            goto(HubWorld_EventScript_Maxie)
        }
    ]
}

script HubWorld_EventScript_Archie {
    speakername("Old Archie")
    msgbox(format("{CREATE_MUGSHOT MUGSHOT_OLD_ARCHIE 0}Need something, kid?"), MSGBOX_DEFAULT)
    dynmultichoice(0, 0, FALSE, 3, 0, DYN_MULTICHOICE_CB_NONE, "Demon Lords?", "Storage device?", "Retraining?")
    switch(var(VAR_RESULT)) {
        case 1:
            msgbox(format(
                "{CREATE_MUGSHOT MUGSHOT_OLD_ARCHIE 0}It's a “PC”. It comes from the future.\p"
                "Back home we use it for a lot of cool stuff, but this one only stores items.\p"
                "Thankfully, all the items I brought with me are intact!"
            ))
            waitmessage
            closemessage
            break
        case 0:
            msgbox(format("{CREATE_MUGSHOT MUGSHOT_OLD_ARCHIE 0}Ah, you wanted to know about the next Demon Lord?"))
            waitmessage
            switch(var(VAR_DEMON_LORD_PROGRESS)){
                case 0:
                    msgbox(format(
                        "{CREATE_MUGSHOT MUGSHOT_OLD_ARCHIE 0}{COLOR RED}Nessereign{COLOR DARK_GRAY}.\nThe first and weakest of the {COLOR RED}Bai'Narii{COLOR DARK_GRAY}.\p"
                        "But don't let this fool you! She's crafty, and most importantly, crazy.\p"
                        "She's swift and cunning when she battles, but also extremely frail.\p"
                        "Nessereign is duplicitous in nature; as such, her minions are Normal and Dark."
                    ))
                    waitmessage
                    closemessage
                case 1:
                    msgbox(format(
                        "{CREATE_MUGSHOT MUGSHOT_OLD_ARCHIE 0}{COLOR RED}Greehaseet{COLOR DARK_GRAY}.\nThe {COLOR RED}Bai'Narii{COLOR DARK_GRAY}'s unrelenting chariot.\p"
                        "Despite his looks, he possesses great strength, but also great stamina\p"
                        "He depletes his opponent's strength, then crushes them!\p"
                        "He rules the depths with his minions of Ice and Water."
                    ))
                    waitmessage
                    closemessage
                case 2:
                    msgbox(format(
                        "{CREATE_MUGSHOT MUGSHOT_OLD_ARCHIE 0}{COLOR RED}Sapprilon{COLOR DARK_GRAY}.\nThe {COLOR RED}Bai'Narii{COLOR DARK_GRAY}'s screaming wall.\p"
                        "He creates forcefields of noise that can repel attacks without much effort.\p"
                        "You'll have to sweat a little to breach his defenses.\p"
                        "He enlists minions he believes can be utilized best: Steel and Psychic."
                    ))
                    waitmessage
                    closemessage
                case 3:
                    speakername("Old Archie")
                    msgbox(format(
                        "{CREATE_MUGSHOT MUGSHOT_OLD_ARCHIE 0}{COLOR RED}Addisamap{COLOR DARK_GRAY}.\nThe wise brain of the {COLOR RED}Bai'Narii{COLOR DARK_GRAY}.\p"
                        "She watches over them and keeps them in check for their master."
                    ))
                    closemessage

                    playse(SE_PIN)
                    applymovement(OBJ_EVENT_ID_PLAYER, moves(emote_question_mark, delay_16 * 6))
                    waitse
                    speakername("Archie")
                    msgbox(format("{CREATE_MUGSHOT MUGSHOT_MC 0}Wait, aren't there only four {COLOR RED}Bai'Narii{COLOR DARK_GRAY}?"))
                    closemessage

                    speakername("Old Archie")
                    msgbox(format(
                        "{CREATE_MUGSHOT MUGSHOT_OLD_ARCHIE 0}No, there's one more, behind them all.\nBut first things first!\p"
                        "The shrewd {COLOR RED}Addisamap{COLOR DARK_GRAY} controls life and death in her own terms.\p"
                        "Make sure that you can outdo the healing she can do to herself.\p"
                        "Her minions are of Grass and Ground."
                    ))
                    waitmessage
                    closemessage
            }
            break
        case 2:
            msgbox(format(
                "{CREATE_MUGSHOT MUGSHOT_OLD_ARCHIE 0}Hm? You want to retrain your Pokémon? Well, it's quite simple.\p"
                "Each Pokémon has its own well of strength it can draw from.\l"
                "Let's call these “effort values”.\p"
                "As their trainer, your responsibility is to train them in the best possible way.\p"
                "Each time you defeat a {COLOR RED}{COLOR RED}Bai'Narii{COLOR DARK_GRAY}{COLOR DARK_GRAY} Lord, your team gets stronger.\p"
                "So you should check on them often and see if they need retraining."
            ))
            waitmessage
            closemessage
    }
    waitmessage
    closemessage
    end
}

script HubWorld_EventScript_NessereignWarp {
    if(!flag(TRAINER_FLAGS_START + TRAINER_DEMONLORD_NESSEREIGN)){
        msgbox(format("It's a marble pedestal.\nIt's missing a statue.\pTouch it?"))
        dynmultichoice(0, 0, FALSE, 3, 0, DYN_MULTICHOICE_CB_NONE, "Yes", "No")
        closemessage
        if(var(VAR_RESULT) == 0) {
            setrespawn(HEAL_LOCATION_DEMON_LAIRS_NESSIE)
            playse(SE_LAVARIDGE_FALL_WARP)
            warpwhitefade(MAP_DEMON_LAIRS_NESSIE, 6, 8)
            waitstate
        } else {}
    } else {
        msgbox("No need to go back.")
        closemessage
    }
    end
}

script HubWorld_EventScript_GreehaseetWarp {
    if(!flag(TRAINER_FLAGS_START + TRAINER_DEMONLORD_GREEHASEET) && flag(TRAINER_FLAGS_START + TRAINER_DEMONLORD_NESSEREIGN)){
        msgbox(format("It's a corroded, porous stone tablet.\pTouch it?"))
        dynmultichoice(0, 0, FALSE, 3, 0, DYN_MULTICHOICE_CB_NONE, "Yes", "No")
        closemessage
        if(var(VAR_RESULT) == 0) {
            setrespawn(HEAL_LOCATION_DEMON_LAIRS_GREEHASEET)
            playse(SE_LAVARIDGE_FALL_WARP)
            warpwhitefade(MAP_DEMON_LAIRS_GREEHASEET, 9, 19)
            waitstate
        } else {}
    } elif(flag(TRAINER_FLAGS_START + TRAINER_DEMONLORD_GREEHASEET) && flag(TRAINER_FLAGS_START + TRAINER_DEMONLORD_NESSEREIGN)) {
        msgbox("No need to go back.")
        closemessage
    } else {
    }
    end
}

script HubWorld_EventScript_SapprilonWarp {
    if(!flag(TRAINER_FLAGS_START + TRAINER_DEMONLORD_SAPPRILON) && flag(TRAINER_FLAGS_START + TRAINER_DEMONLORD_GREEHASEET) && flag(TRAINER_FLAGS_START + TRAINER_DEMONLORD_NESSEREIGN)){
        msgbox(format("It's a wooden box with a waxen disc on it, spinning under a needle.\pTouch it?"))
        dynmultichoice(0, 0, FALSE, 3, 0, DYN_MULTICHOICE_CB_NONE, "Yes", "No")
        closemessage
        if(var(VAR_RESULT) == 0) {
            setrespawn(HEAL_LOCATION_DEMON_LAIRS_SAPPRILON)
            playse(SE_LAVARIDGE_FALL_WARP)
            warpwhitefade(MAP_DEMON_LAIRS_SAPPRILON, 6, 12)
            waitstate
        } else {}
    } elif(flag(TRAINER_FLAGS_START + TRAINER_DEMONLORD_SAPPRILON) && flag(TRAINER_FLAGS_START + TRAINER_DEMONLORD_GREEHASEET) && flag(TRAINER_FLAGS_START + TRAINER_DEMONLORD_NESSEREIGN)) {
        msgbox("No need to go back.")
        closemessage
    } else {
    }
    end
}

script HubWorld_EventScript_AddisamapWarp {
    if(var(VAR_DEMON_LORD_PROGRESS) == 3){
        msgbox(format("It's an enormous globe: a diorama\nof {COLOR RED}R'mhakking{COLOR DARK_GRAY}.\pTouch it?"))
        dynmultichoice(0, 0, FALSE, 3, 0, DYN_MULTICHOICE_CB_NONE, "Yes", "No")
        closemessage
        if(var(VAR_RESULT) == 0) {
            setrespawn(HEAL_LOCATION_DEMON_LAIRS_ADDISAMAP)
            playse(SE_LAVARIDGE_FALL_WARP)
            warpwhitefade(MAP_DEMON_LAIRS_ADDISAMAP, 12, 27)
            waitstate
        }
    } elif(flag(TRAINER_FLAGS_START + TRAINER_DEMONLORD_ADDISAMAP) && flag(TRAINER_FLAGS_START + TRAINER_DEMONLORD_SAPPRILON) && flag(TRAINER_FLAGS_START + TRAINER_DEMONLORD_GREEHASEET) && flag(TRAINER_FLAGS_START + TRAINER_DEMONLORD_NESSEREIGN)) {
        msgbox("No need to go back.")
        closemessage
    } else {
    }
    end
}

script HubWorld_EventScript_Maxie {
    lockall
    setrespawn(HEAL_LOCATION_DEMON_LAIRS_MAXIE)
    setvar(VAR_DEMON_LORD_PROGRESS, 5)
    special(MaxieReboot)
    autosave

    applymovement(LOCALID_PLAYER, moves(walk_up * 4))
    waitmovement(0)
    playse(SE_PIN)
    applymovement(LOCALID_PLAYER, moves(emote_happy))

    speakername("Archie")
    msgbox(format(
        "{CREATE_MUGSHOT MUGSHOT_MC 0}Old Archie! Old Archie! I did it!\nI've defeated all four of them!"
    ))
    closemessage

    applymovement(LOCALID_HUBWORLD_ARCHIE, moves(walk_in_place_fast_down))

    speakername("Old Archie")
    msgbox(format(
        "{CREATE_MUGSHOT MUGSHOT_OLD_ARCHIE 0}Whaddyawant, a medal or something?\nYour job ain't done! There's one left!\p"
        "The one who reigns above all.\nHis power has no equal.\p"
        "You have to be as prepared as possible to stand a chance against him.\p"
        "We should go back home, and get a couple more-"
    ))
    closemessage

    playse(SE_M_THUNDERBOLT2)
	setvar(VAR_0x8004, 0)  // vertical pan
	setvar(VAR_0x8005, 1)  // horizontal pan
	setvar(VAR_0x8006, 5)  // num shakes
	setvar(VAR_0x8007, 3)  // shake delay
	special(ShakeCamera)
    waitstate

    delay(48)

    applymovement(LOCALID_PLAYER, moves(face_down))

    speakername("Archie")
    msgbox("{CREATE_MUGSHOT MUGSHOT_MC 0}…Huh?")
    closemessage

    playse(SE_PIN)
    applymovement(LOCALID_HUBWORLD_ARCHIE, moves(emote_sweat))
    speakername("Old Archie")
    msgbox("{CREATE_MUGSHOT MUGSHOT_OLD_ARCHIE 0}Oh no no no…")
    waitmessage
    closemessage

    playse(SE_M_EARTHQUAKE)
    setvar(VAR_0x8004, 0)  // vertical pan
	setvar(VAR_0x8005, 4)  // horizontal pan
	setvar(VAR_0x8006, 6501)  // num shakes
	setvar(VAR_0x8007, 2)  // shake delay
	special(ShakeCamera)

    speakername("???")
    msgbox(
        "I'VE FOUND YOU, INTERLOPERS.\nYOU THOUGHT YOU COULD HIDE FROM ME?\p"
        "YOU'VE MADE A TERRIBLE MISTAKE!\p"
        "SAY YOUR PRAYERS…{PAUSE 8}\nWHILE YOU CAN STILL SPEAK AT ALL!\p"
        "HAHAHAHAHAHAHAHAHAHAHAHAHAHAHAHAHA\nHAHAHAHAHAHAHAHAHAHAHAHAHAHAHAHAHA"
    )
    waitmessage

    callnative(0)
}
