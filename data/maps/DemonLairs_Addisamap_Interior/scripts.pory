mapscripts DemonLairs_Addisamap_Interior_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_0, 0 {
            call(DemonLairs_EventScript_Addisamap)
        }
    ]
    MAP_SCRIPT_ON_TRANSITION {
        if(!flag(FLAG_ADDISAMAP_PUZZLE_SOLVED)){
            setflag(FLAG_ADDISAMAP_PUZZLE_SOLVED)
        }
    }
}

script DemonLairs_EventScript_Addisamap {
    lockall

    autosave

    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up * 8))
    waitmovement(OBJ_EVENT_ID_PLAYER)

    if(!flag(FLAG_TALKED_TO_ADDISAMAP)){
        setflag(FLAG_TALKED_TO_ADDISAMAP)

        playse(SE_PIN)
        applymovement(LOCALID_ADDISAMAP_RUIN_MANIAC, moves(emote_pensive))
        waitmovement(LOCALID_ADDISAMAP_RUIN_MANIAC)

        speakername("Addisamap")
        msgbox(format(
            "{CREATE_MUGSHOT MUGSHOT_ADDISAMAP 0}The troublemaker finally got here, hmm?\n"
            "Color me impressed."
        ))
        closemessage

        playse(SE_LEDGE)
        applymovement(LOCALID_PLAYER, moves(jump_in_place_up))
        speakername("Archie")
        msgbox(format("{CREATE_MUGSHOT MUGSHOT_MC 0}You sound calm, when you should be panicking!"))
        closemessage

        playse(SE_PIN)
        applymovement(LOCALID_ADDISAMAP_RUIN_MANIAC, moves(emote_music))
        speakername("Addisamap")
        msgbox(format(
            "{CREATE_MUGSHOT MUGSHOT_ADDISAMAP 0}What reason do I have to be worried?\nI am as ancient as time itself.\p"
            "The ground the young man treads on\nwas crafted by these hands.\p"
            "Does he think he can defeat the weight of the centuries?\p"
            "Ho ho ho…\p"
            "He is ultimately failing this silly\nquest of his.\NEither by my hand, or by our Master's."
        ))
        closemessage

        playse(SE_PIN)
        applymovement(LOCALID_PLAYER, moves(emote_angry, walk_in_place_up))
        speakername("Archie")
        msgbox(format("{CREATE_MUGSHOT MUGSHOT_MC 0}Who's this Master of yours?!\nTell me his name!"))
        closemessage

        applymovement(LOCALID_ADDISAMAP_RUIN_MANIAC, moves(face_up, delay_16, emote_pensive, delay_16 * 3, face_down))
        delay(16)
        playse(SE_PIN)
        waitmovement(LOCALID_ADDISAMAP_RUIN_MANIAC)

        speakername("Addisamap")
        msgbox(format(
            "{CREATE_MUGSHOT MUGSHOT_ADDISAMAP 0}I shan't. The young man doesn't deserve to hear His name.\p"
            "It's a shame, really…\p"
            "He will never have the chance to meet Him."
        ))
        closemessage
    } else {
        speakername("Addisamap")
        msgbox(format(
            "{CREATE_MUGSHOT MUGSHOT_ADDISAMAP 0}The quester returns.\nStubborn as ever.\p"
            "He will soon realize my patience is infinite."
        ))
        waitmessage
        closemessage
    }

    trainerbattle_no_intro(TRAINER_DEMONLORD_ADDISAMAP, format("… … … … … …\n… … … … … …"))

    playbgm(MUS_RAYQUAZA_APPEARS, TRUE)

    playse(SE_PIN)
    applymovement(LOCALID_ADDISAMAP_RUIN_MANIAC, moves(emote_pensive))
    speakername("Addisamap")
    msgbox(format(
        "{CREATE_MUGSHOT MUGSHOT_ADDISAMAP 0}… … … … … …\n… … … … … …\p"
        "Very well. It seems my time has come.\nBut the boy's is fast approaching."
    ))

    playse(SE_M_THUNDERBOLT2)
	setvar(VAR_0x8004, 0)  // vertical pan
	setvar(VAR_0x8005, 1)  // horizontal pan
	setvar(VAR_0x8006, 5)  // num shakes
	setvar(VAR_0x8007, 3)  // shake delay
	special(ShakeCamera)

    speakername("Addisamap")
    msgbox(format(
        "{CREATE_MUGSHOT MUGSHOT_ADDISAMAP 0}He should consider turning back now, unless he wishes to die in regret."
    ))

    playse(SE_M_THUNDERBOLT2)
	setvar(VAR_0x8004, 0)  // vertical pan
	setvar(VAR_0x8005, 1)  // horizontal pan
	setvar(VAR_0x8006, 8)  // num shakes
	setvar(VAR_0x8007, 3)  // shake delay
	special(ShakeCamera)

    speakername("Addisamap")
    msgbox(format(
        "{CREATE_MUGSHOT MUGSHOT_ADDISAMAP 0}Now…\nI return to the Earth I made."
    ))
    closemessage

    applymovement(LOCALID_ADDISAMAP_RUIN_MANIAC, moves(shake_horizontal))
    playbgm(MUS_DUMMY, TRUE)
    playmoncry(SPECIES_ADDISAMAP, CRY_MODE_WEAK)
    waitmovement(LOCALID_ADDISAMAP_RUIN_MANIAC)

    fadescreen(FADE_TO_WHITE)

    removeobject(LOCALID_ADDISAMAP_RUIN_MANIAC)

    delay(60)

    fadescreen(FADE_FROM_WHITE)

    delay(60)

    clearflag(FLAG_SYS_BOSS_IN_PROG)

    playse(SE_PC_ON)

    applymovement(LOCALID_PLAYER, moves(face_down))

    setmetatile(12, 13, 0x026, FALSE)
    special(DrawWholeMapView)

    waitse

    setvar(VAR_TEMP_0, 1)

    releaseall
}
