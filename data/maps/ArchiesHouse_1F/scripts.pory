mapscripts ArchiesHouse_1F_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_ARCHIE_ARRIVES, 0 {
            call(ArchiesHouse_EventScript_BeginningCutscene)
        }
    ]
    MAP_SCRIPT_ON_TRANSITION {
        removeobject(OBJ_FUTUREARCHIE)
        if(var(VAR_ARCHIE_ARRIVES) > 0){clearflag(FLAG_HIDE_ARCHIE)}
        else {}
    }
}

script ArchiesHouse_EventScript_BeginningCutscene {
    speakername("Archie")
    msgbox(format("…\p Sure is nice to have such a peaceful life…\pI sure hope nothing comes in to ruin it."))
    closemessage
    delay(60)
    playbgm(MUS_DUMMY, FALSE)
    playse(SE_M_THUNDERBOLT2)
    fadescreen(FADE_TO_WHITE)
    waitse
    fadescreen(FADE_FROM_WHITE)
    clearflag(FLAG_HIDE_ARCHIE)
    addobject(OBJ_FUTUREARCHIE)
    playbgm(MUS_WEATHER_GROUDON, FALSE)
    speakername("Archie")
    msgbox("What the?!")
    closemessage
    applymovement(OBJ_FUTUREARCHIE, moves(walk_up * 4, walk_right * 3))
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_left))
    waitmovement(OBJ_FUTUREARCHIE)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    speakername("Archie")
    msgbox("Who are you…?")
    applymovement(OBJ_FUTUREARCHIE, moves(walk_in_place_fast_right))
    speakername("Old Archie")
    msgbox(format("We don't have time."))
    waitmovement(0)
    msgbox(format("I'm you from a different future. One where you're free from the Bai'Narii, and I've come here to help you."))
    speakername("Archie")
    msgbox(format("Defeat the Bai'Narii? But… how?"))
    speakername("Old Archie")
    msgbox(format("You have to trust me. But you can't go alone. I know you have your friends outside, so go get whoever you want, and come back to me."))
    closemessage
    setvar(VAR_ARCHIE_ARRIVES, 1)
    end
}

script ArchiesHouse_EventScript_Archie {
    lock
    faceplayer
    getpartysize
    copyvar(VAR_TEMP_0, VAR_RESULT)
    speakername("Old Archie")
    msgbox(format("So, are you ready to go face the Demon Lords?"), MSGBOX_YESNO)
    waitmessage
    if(var(VAR_RESULT) == YES){
        if(var(VAR_TEMP_0) > 0)
        {
            msgbox(format("Well, follow me then! I'll use this device to transport us…"))
            waitmessage
            closemessage
            warpwhitefade(MAP_HUB_WORLD_INITIAL, 8, 4)
        } else {
            msgbox(format("Don't lie to me with that stupid grin! You have no Pokémon to battle them with!"),MSGBOX_DEFAULT)
            closemessage
        }
    }
    elif(var(VAR_RESULT) == NO) {
        msgbox(format("Hurry up, then! The fate of the world is waiting!"), MSGBOX_DEFAULT)
        closemessage
    }
    applymovement(0, moves(face_original_direction))
    waitmovement(0)
    end
}